package require tcltest
eval ::tcltest::configure $argv
# Ensure package is loaded from ./package rather than /usr/lib/tcltk
set auto_path [linsert $auto_path 0 ./package]
package require qcode
package require Pgtcl

namespace eval ::qcode::test {
    namespace import ::tcltest::*
    namespace path ::qc
    testConstraint requires_database true

    # The test user configuration.
    set test_conf {
        host localhost
        port 5432
        dbname test_database
        user test_user
        password test_password
    }

    # Request a username/password in order to set up test environment
    # User must have sufficient privileges (create user, create db)
    puts "Enter username for database:"
    set user [gets stdin]
    puts "Enter password for database:"
    exec stty -echo
    set password [gets stdin]
    exec stty echo

    # Attempt to setup a database and user for testing
    set conn [pg_connect -connlist "
        host localhost
        port 5432
        dbname template1
        user $user
        password $password
    "]
    set failed [catch {
        pg_execute $conn {CREATE DATABASE test_database}
        pg_execute $conn {
            CREATE USER test_user WITH PASSWORD 'test_password';
            GRANT ALL PRIVILEGES ON DATABASE test_database TO test_user;
        }
    } results options]
    pg_disconnect $conn
    if { $failed } {
        return -options $options $results
    }
    # load pgcrypto.sql for test_database (must be done as superuser)
    regexp {PostgreSQL\s+([0-9]+\.[0-9]+)\.([0-9]+)} [exec pg_config --version] -> major_version minor_version
    if { $major_version >= 9.1 } {
        set pgcrypto "CREATE EXTENSION pgcrypto"
    } else {
        set pgcrypto [read [open [exec pg_config --sharedir]/contrib/pgcrypto.sql r]]
    }
    set conn [pg_connect -connlist "
        host localhost
        port 5432
        dbname test_database
        user $user
        password $password
    "]
    set failed [catch {
        pg_execute $conn $pgcrypto
    } results options]
    pg_disconnect $conn
    if { $failed } {
        return -options $options $results
    }

    # Create setup and cleanup scripts for tests
    set setup {
        # Establish a connection the qc::db way
        db_connect {*}$test_conf

        # Encryption key for testing
        set key secretkey
    }
    set cleanup {
        # Cleanup the qc::db connection
        pg_disconnect $_db
        unset _db

        # Cleanup vars
        unset key
    }

    # Catch errors in testing so that the database can always be cleaned up after
    set testing_failed [catch {
        # ========================================
        # Begin the actual testing
        # ========================================
        # pkcs_padding_append
        test pkcs_padding_append-1.0 {pkcs_padding_append - 1 byte unicode} -setup $setup -cleanup $cleanup -constraints {requires_database} -body {
            set list [list \
                          [string repeat [format %c 0] 0] \
                          [string repeat [format %c 0] 1] \
                          [string repeat [format %c 0] 2] \
                          [string repeat [format %c 0] 3] \
                          [string repeat [format %c 0] 4] \
                          [string repeat [format %c 0] 5] \
                          [string repeat [format %c 0] 6] \
                          [string repeat [format %c 0] 7] \
                          [string repeat [format %c 0] 8] \
                         ]
            set results {}
            foreach string $list {
                lappend results [pkcs_padding_append $string]   
            }
            return $results
        } -result [list \
                       [string repeat [format %c 0] 0][string repeat [format %c 8] 8] \
                       [string repeat [format %c 0] 1][string repeat [format %c 7] 7] \
                       [string repeat [format %c 0] 2][string repeat [format %c 6] 6] \
                       [string repeat [format %c 0] 3][string repeat [format %c 5] 5] \
                       [string repeat [format %c 0] 4][string repeat [format %c 4] 4] \
                       [string repeat [format %c 0] 5][string repeat [format %c 3] 3] \
                       [string repeat [format %c 0] 6][string repeat [format %c 2] 2] \
                       [string repeat [format %c 0] 7][string repeat [format %c 1] 1] \
                       [string repeat [format %c 0] 8][string repeat [format %c 8] 8] \
                      ]
        
        test pkcs_padding_append-1.1 {pkcs_padding_append - 2 byte unicode} -setup $setup -cleanup $cleanup -constraints {requires_database} -body {
           set list [list \
                         [string repeat [format %c 256] 0] \
                         [string repeat [format %c 256] 1] \
                         [string repeat [format %c 256] 2] \
                         [string repeat [format %c 256] 3] \
                         [string repeat [format %c 256] 4] \
                         [string repeat [format %c 256] 5] \
                         [string repeat [format %c 256] 6] \
                         [string repeat [format %c 256] 7] \
                         [string repeat [format %c 256] 8] \
                        ]
            set results {}
            foreach string $list {
                lappend results [pkcs_padding_append $string]   
            } 
            return $results
        } -result [list \
                       [string repeat [format %c 256] 0][string repeat [format %c 8] 8] \
                       [string repeat [format %c 256] 1][string repeat [format %c 7] 7] \
                       [string repeat [format %c 256] 2][string repeat [format %c 6] 6] \
                       [string repeat [format %c 256] 3][string repeat [format %c 5] 5] \
                       [string repeat [format %c 256] 4][string repeat [format %c 4] 4] \
                       [string repeat [format %c 256] 5][string repeat [format %c 3] 3] \
                       [string repeat [format %c 256] 6][string repeat [format %c 2] 2] \
                       [string repeat [format %c 256] 7][string repeat [format %c 1] 1] \
                       [string repeat [format %c 256] 8][string repeat [format %c 8] 8] \
                      ]

        # pkcs_padding_strip            
        test pkcs_padding_strip-1.0 {pkcs_padding_append} -setup $setup -cleanup $cleanup -constraints {requires_database} -body {
            set list [list \
                          [string repeat [format %c 0] 0][string repeat [format %c 8] 8] \
                          [string repeat [format %c 0] 1][string repeat [format %c 7] 7] \
                          [string repeat [format %c 0] 2][string repeat [format %c 6] 6] \
                          [string repeat [format %c 0] 3][string repeat [format %c 5] 5] \
                          [string repeat [format %c 0] 4][string repeat [format %c 4] 4] \
                          [string repeat [format %c 0] 5][string repeat [format %c 3] 3] \
                          [string repeat [format %c 0] 6][string repeat [format %c 2] 2] \
                          [string repeat [format %c 0] 7][string repeat [format %c 1] 1] \
                          [string repeat [format %c 0] 8][string repeat [format %c 8] 8] \
                         ]
            set results {}
            foreach string $list {
                lappend results [pkcs_padding_strip $string]   
            }
            return $results
        } -result [list \
                       [string repeat [format %c 0] 0] \
                       [string repeat [format %c 0] 1] \
                       [string repeat [format %c 0] 2] \
                       [string repeat [format %c 0] 3] \
                       [string repeat [format %c 0] 4] \
                       [string repeat [format %c 0] 5] \
                       [string repeat [format %c 0] 6] \
                       [string repeat [format %c 0] 7] \
                       [string repeat [format %c 0] 8] \
                      ]

        # encrypt_bf_db
        test encrypt_bf_db-1.0 {encrypt_bf_db - encrypt empty string} -setup $setup -cleanup $cleanup -constraints {requires_database} -body {
            encrypt_bf_db $key ""
        } -result {}

        test encrypt_bf_db-1.1 {encrypt_bf_db - plaintext -> ciphertext -> plaintext} -setup $setup -cleanup $cleanup -constraints {requires_database} -body {
            set i 0
            set string ""
            while { $i <= 300 } {
                append string [format %c $i]   
                if { $i % 10 == 0 } {
                    set r1 [encrypt_bf_db $key $string]
                    set r2 [decrypt_bf_db $key $r1]
                    if { $r2 ne $string } {
                        # Return after first error
                        return "Unable to successfully encrypt and decrypt \"$string\""
                    }                
                }
                incr i
            }
            return ""
        } -result {}
        

        # encrypt_bf_tcl
        test encrypt_bf_tcl-1.0 {encrypt_bf_tcl - encrypt empty string} -setup $setup -cleanup $cleanup -constraints {requires_database} -body {
            encrypt_bf_tcl $key ""
        } -result {}

        test encrypt_bf_tcl-1.1 {encrypt_bf_tcl - plaintext -> ciphertext -> plaintext} -setup $setup -cleanup $cleanup -constraints {requires_database} -body {
            set i 0
            set string ""
            while { $i <= 300 } {
                append string [format %c $i]  
                if { $i % 10 == 0 } { 
                    set r1 [encrypt_bf_tcl $key $string]
                    set r2 [decrypt_bf_tcl $key $r1]
                    if { $r2 ne $string } {
                        # Return after first error
                        return "Unable to successfully encrypt and decrypt \"$string\""
                    }                
                }
                incr i
            }
            return ""
        } -result {}

        test encrypt_bf_tcl-1.2 {encrypt_bf-tcl - check compatibility with encrypt_bf_db} -setup $setup -cleanup $cleanup -constraints {requires_database} -body {
            set i 0
            set string ""
            while { $i <= 300 } {
                if { $i % 10 == 0 } {
                    append string [format %c $i]   
                    set r1 [encrypt_bf_tcl $key $string]
                    set r2 [encrypt_bf_db $key $string]
                    if { [trim $r1] ne [trim $r2] } {
                        # Return after first error
                        return "Unable to successfully encrypt and decrypt \"$string\""
                    }                
                }
                incr i
            }
            return ""
        } -result {}

        # ========================================
        # End of testing - now do cleanup
        # ========================================
    } testing_fail_results testing_fail_options]

    # Attempt to drop the database and user that were created for testing
    set conn [pg_connect -connlist "
        host localhost
        port 5432
        dbname template1
        user $user
        password $password
    "]
    set database_cleanup_failed [catch {
        pg_execute $conn {DROP DATABASE IF EXISTS test_database}
        pg_execute $conn {
            DROP ROLE IF EXISTS test_user;
        }
    } database_cleanup_results database_cleanup_options]
    pg_disconnect $conn

    if { $testing_failed } {
        return -options $testing_fail_options $testing_fail_results
    }

    if { $database_cleanup_failed } {
        return -options $database_cleanup_options $database_cleanup_results
    }

    cleanupTests
}
namespace delete ::qcode::test
