package require tcltest
eval ::tcltest::configure $argv

# Load all .tcl files
set files [lsort [glob -nocomplain [file join "../tcl" *.tcl]]]
foreach file $files {
    source $file
}

namespace eval ::qcode::test {
    namespace import ::tcltest::*
    namespace path ::qc

    test tablular_text_parse-1.0 {tablular_text_parse - basic } -setup {
    } -body {
        set lines {}
        lappend lines "Column 1 Col2"
        lappend lines "a        b      "
        lappend lines "c        d      "
        set text [join $lines \n]

        set conf {}
        lappend conf [list label "Column 1" var_name "col1"]
        lappend conf [list label "Col2" var_name "column_2"]
        return [qc::tabular_text_parse $text $conf]
    } -cleanup {} -result {{col1 column_2} {a b} {c d}}

    test tablular_text_parse-2.0 {tablular_text_parse - column name ending with % } -setup {
    } -body {
        set lines {}
        lappend lines "Column 1% Column 2"
        lappend lines "a         b      "
        set text [join $lines \n]

        set conf {}
        lappend conf [list label "Column 1%" var_name "col1"]
        lappend conf [list label "Column 2" var_name "col2"]
        return [qc::tabular_text_parse $text $conf]
    } -cleanup {} -result {{col1 col2} {a b}}


    test tablular_text_parse-3.0 {tablular_text_parse - column name ending with . } -setup {
    } -body {
        set lines {}
        lappend lines {ITEM NO.         EXT. PRICE   TOTAL}
        lappend lines {000200           1.20         140}
        set text [join $lines \n]

        set conf {}
        lappend conf [list label "ITEM NO." var_name "item_number"]
        lappend conf [list label "EXT. PRICE" var_name "ext_price"]
        lappend conf [list label "TOTAL" var_name "total"]
        return [qc::tabular_text_parse $text $conf]
    } -cleanup {} -result {{item_number ext_price total} {000200 1.20 140}}

    test tablular_text_parse-4.0 {tablular_text_parse - optional first column } -setup {
    } -body {
        set lines {}
        lappend lines {ITEM NO.         EXT. PRICE   TOTAL}
        lappend lines {1                1.20         140}
        lappend lines {                 1.30         150}
        set text [join $lines \n]

        set conf {}
        lappend conf [list label "ITEM NO." var_name "item_number"]
        lappend conf [list label "EXT. PRICE" var_name "ext_price"]
        lappend conf [list label "TOTAL" var_name "total"]
        return [qc::tabular_text_parse $text $conf]
    } -cleanup {} -result {{item_number ext_price total} {1 1.20 140} {{} 1.30 150}}

    test tablular_text_parse-5.0 {tablular_text_parse - don't ignore empty rows } -setup {
    } -body {
        set lines {}
        lappend lines {ITEM NO.         EXT. PRICE   TOTAL}
        lappend lines {1                1.20         140}
        lappend lines {}
        lappend lines {2                1.40         160}
        lappend lines {}
        set text [join $lines \n]

        set conf {}
        lappend conf [list label "ITEM NO." var_name "item_number"]
        lappend conf [list label "EXT. PRICE" var_name "ext_price"]
        lappend conf [list label "TOTAL" var_name "total"]
        return [qc::tabular_text_parse $text $conf]
    } -cleanup {} -result {{item_number ext_price total} {1 1.20 140} {{} {} {}} {2 1.40 160} {{} {} {}}}
    
    test tablular_text_parse-6.0 {tablular_text_parse - ignore empty rows } -setup {
    } -body {
        set lines {}
        lappend lines {ITEM NO.         EXT. PRICE   TOTAL}
        lappend lines {1                1.20         140}
        lappend lines {}
        lappend lines {2                1.40         160}
        lappend lines {}
        set text [join $lines \n]

        set conf {}
        lappend conf [list label "ITEM NO." var_name "item_number"]
        lappend conf [list label "EXT. PRICE" var_name "ext_price"]
        lappend conf [list label "TOTAL" var_name "total"]
        return [qc::tabular_text_parse -ignore_empty_rows $text $conf]
    } -cleanup {} -result {{item_number ext_price total} {1 1.20 140} {2 1.40 160}}

    test tablular_text_parse-7.0 {tablular_text_parse - ignore empty rows } -setup {
    } -body {
        set lines {}
        lappend lines {Code         Qty   TOTAL}
        lappend lines {ABCDEF       1     1.10}
        set text [join $lines \n]

        set conf {}
        lappend conf [list label "Code" var_name "code"]
        lappend conf [list label "Qty" var_name "qty"]
        lappend conf [list label "TOTAL" var_name "total"]
        return [qc::tabular_text_parse $text $conf]
    } -cleanup {} -result {{code qty total} {ABCDEF 1 1.10}}

    test tablular_text_parse-8.0 {tablular_text_parse - unable to find locate column error } -setup {
    } -body {
        set lines {}
        lappend lines {Code         Qty   TOTAL}
        lappend lines {1            1     1.10}
        set text [join $lines \n]

        set conf {}
        lappend conf [list label "OrderCode" var_name "code"]
        lappend conf [list label "Qty" var_name "qty"]
        lappend conf [list label "TOTAL" var_name "total"]
        return [qc::tabular_text_parse $text $conf]
    } -cleanup {} -result {Unable to locate column heading "OrderCode"} -returnCodes error 

    test tabular_text_parse-9.0 {tabular_text_parse - disable auto trim} -setup {
    } -body {
        set lines {}
        lappend lines {Code         Qty   TOTAL}
        lappend lines { 1           1     1.10}
        set text [join $lines \n]

        set conf {}
        lappend conf [list label "Code" var_name "code" trim false]
        lappend conf [list label "Qty" var_name "qty" trim true]
        lappend conf [list label "TOTAL" var_name "total"]
        return [qc::tabular_text_parse $text $conf]
    } -cleanup {} -result {{code qty total} {{ 1  } 1 1.10}}
    
    cleanupTests
}
namespace delete ::qcode::test
